plugins {
    id 'org.springframework.boot' version '4.0.0'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'java'
    id 'jacoco'
}

group = 'sk.cyrilgavala'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '21'
    targetCompatibility = '21'
}

repositories {
    mavenCentral()
}

dependencies {
    // Annotation Processors
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    // Spring Boot Starters
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // Jackson
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8'

    // Lombok
    implementation 'org.projectlombok:lombok'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.12.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.5'

    // Mapping
    implementation 'org.modelmapper:modelmapper:3.2.0'

    // API Documentation
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:3.0.0'

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}

test {
    useJUnitPlatform()
    jvmArgs = [
            '-XX:+EnableDynamicAgentLoading' // Suppress Java 21 dynamic agent loading warning
    ]
    finalizedBy jacocoTestReport // Generate coverage report after tests run
}

jacoco {
    toolVersion = "0.8.12"
}

jacocoTestReport {
    dependsOn test // Tests are required to run before generating the report
    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/config/**',
                    '**/handler/*.java',
                    '**/model/**',
                    '**/dto/**',
                    '**/WardrobeApiApplication.class',
                    '**/JwtAuthenticationFilter.class'
            ])
        }))
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 95.0
            }
        }
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/config/**',
                    '**/handler/*.java',
                    '**/model/**',
                    '**/dto/**',
                    '**/WardrobeApiApplication.class',
                    '**/JwtAuthenticationFilter.class'
            ])
        }))
    }
}

tasks.withType(JavaExec) {
    jvmArgs = [
            '-XX:+EnableDynamicAgentLoading' // Suppress Java 21 dynamic agent loading warning
    ]
}